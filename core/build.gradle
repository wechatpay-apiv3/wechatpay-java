import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins{
    id "common"
}

ext {
    publishedArtifactId = "wechatpay-java-core"
}

jar {
    manifest {
        attributes("Automatic-Module-Name": "com.wechat.pay.java.core")
        attributes("Implementation-Version": project.version)
    }
}

dependencies {
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoInlineVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"

    testImplementation platform("org.junit:junit-bom:${junit5Version}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'

    testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

publishing {
    java {
        withJavadocJar()
        withSourcesJar()
    }

    publications {
        maven(MavenPublication) {
            artifactId = project.ext.publishedArtifactId
            from components.java

            pom {
                name = project.ext.publishedArtifactId
                description = "A Java Core SDK for WeChat Pay\'s API"
                url = "https://github.com/wechatpay-apiv3/wechatpay-java"
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "WeChat Pay APIv3 Team"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/wechatpay-apiv3/wechatpay-java.git"
                    developerConnection = "scm:git:ssh://github.com:wechatpay-apiv3/wechatpay-java.git"
                    url = "https://github.com/wechatpay-apiv3/wechatpay-java"
                }
            }
        }
    }

    repositories {
        maven {
            name = "mavencentral"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("SONATYPE_NEXUS_USERNAME")
                password System.getenv("SONATYPE_NEXUS_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey= System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}
